import com.nighthawkapps.wallet.android.Deps

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

archivesBaseName = 'nighthawk-android-wallet'
group = 'com.nighthawkapps.wallet.android'
version = Deps.versionName
android {
    compileSdkVersion Deps.compileSdkVersion

    defaultConfig {
        applicationId Deps.packageName
        minSdkVersion Deps.minSdkVersion
        targetSdkVersion Deps.targetSdkVersion
        versionCode = Deps.versionCode
        versionName = Deps.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        if (Boolean.parseBoolean(isUseTestOrchestrator)) {
            testInstrumentationRunnerArguments clearPackageData: 'true'
        }        multiDexEnabled true
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions 'network'
    lintOptions{
        disable 'MissingTranslation'
    }
    productFlavors {
        // would rather name them "testnet" and "mainnet" but product flavor names cannot start with the word "test"
        zcashtestnet {
            dimension 'network'
            applicationId 'com.nighthawkapps.wallet.android.testnet'
            buildConfigField "String", "DEFAULT_SERVER_URL", '"testnet.lightwalletd.com"'
            matchingFallbacks = ['zcashtestnet', 'debug']
        }

        zcashmainnet {
            dimension 'network'
            buildConfigField "String", "DEFAULT_SERVER_URL", '"mainnet.lightwalletd.com"'
            matchingFallbacks = ['zcashmainnet', 'release']
        }
    }
    signingConfigs {
        nighthawkpublic {
            storeFile file("${rootProject.projectDir}/nighthawkpublic.keystore")
            keyAlias "key0"
            keyPassword "android"
            storePassword "android"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.nighthawkpublic
            buildConfigField "String", "MOON_PAY_KEY", '"pk_live_SWNmrQMVF1fkyfdkQAX6nbzA2sCUXpMk"'
            buildConfigField "String", "MOON_PAY_BASE_URL", '"https://buy.moonpay.com/"'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "MOON_PAY_KEY", '"pk_test_2biYUwnJvOdHFel9jbvvUHorZlYg3dio"'
            buildConfigField "String", "MOON_PAY_BASE_URL", '"https://buy-staging.moonpay.com/"'
        }
    }

    compileOptions {
        // enable support for new language APIs but also fix the issue with zxing on API < 24
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlin.time.ExperimentalTime"
    }
    testOptions {
        if (Boolean.parseBoolean(isUseTestOrchestrator)) {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }
    kapt {
        arguments {
            arg 'dagger.fastInit', 'enabled'
            arg 'dagger.fullBindingGraphValidation', 'ERROR'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$archivesBaseName-v${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }
}

apply from: "$rootDir/ktlint.gradle"
preBuild.dependsOn('ktlintFormat')
preBuild.dependsOn('ktlint')

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':qrecycler')
    implementation project(':mnemonic')
    implementation project(':lockbox')

    // Zcash
    implementation Deps.Zcash.ANDROID_WALLET_PLUGINS
    implementation Deps.Zcash.SDK

    //  Kotlin
    implementation Deps.Kotlin.STDLIB

    // Android
    implementation Deps.AndroidX.ANNOTATION
    implementation Deps.AndroidX.APPCOMPAT
    implementation Deps.AndroidX.BIOMETRICS
    implementation Deps.AndroidX.CONSTRAINT_LAYOUT
    implementation Deps.AndroidX.CORE_KTX
    implementation Deps.AndroidX.FRAGMENT_KTX
    implementation Deps.AndroidX.LEGACY
    implementation Deps.AndroidX.PAGING
    implementation Deps.AndroidX.RECYCLER
    implementation Deps.AndroidX.CameraX.CAMERA2
    implementation Deps.AndroidX.CameraX.CORE
    implementation Deps.AndroidX.CameraX.LIFECYCLE
    implementation Deps.AndroidX.CameraX.View.EXT
    implementation Deps.AndroidX.CameraX.View.VIEW
    implementation Deps.AndroidX.Lifecycle.LIFECYCLE_RUNTIME_KTX
    implementation Deps.AndroidX.Navigation.FRAGMENT_KTX
    implementation Deps.AndroidX.Navigation.UI_KTX
    implementation Deps.AndroidX.Room.ROOM_KTX
    kapt Deps.AndroidX.Room.ROOM_COMPILER
    implementation Deps.AndroidX.SECURITY
    coreLibraryDesugaring Deps.AndroidX.DESUGAR_JDK
    implementation Deps.AndroidX.CUSTOM_CHROME_TABS
    implementation Deps.AndroidX.SPLASH_SCREEN
    implementation Deps.AndroidX.OSS_LICENCE
    implementation Deps.AndroidX.WORK_MANAGER_KTX

    // Google
    implementation Deps.Google.GUAVA
    implementation Deps.Google.MATERIAL
    implementation Deps.Google.GSON

    // Dagger
    implementation Deps.Dagger.ANDROID_SUPPORT
    kapt Deps.Dagger.ANDROID_PROCESSOR
    kapt Deps.Dagger.COMPILER

    // grpc-java
    implementation Deps.Grpc.ANDROID
    implementation Deps.Grpc.OKHTTP
    implementation Deps.Grpc.PROTOBUG
    implementation Deps.Grpc.STUB
    implementation Deps.JavaX.JAVA_ANNOTATION

    // Network
    implementation Deps.Network.RETROFIT
    implementation Deps.Network.RETROFIT_GSON
    implementation Deps.Network.OKHTTP_LOGGING

    // Misc.
    implementation Deps.Misc.PDF_BOX
    implementation Deps.Misc.LOTTIE
    implementation Deps.Misc.Plugins.QR_SCANNER

    // Tests
    testImplementation Deps.Test.COROUTINES_TEST
    testImplementation Deps.Test.JUNIT
    testImplementation Deps.Test.MOKITO
    testImplementation Deps.Test.MOCKITO_KOTLIN
    androidTestImplementation Deps.Test.Android.CORE
    androidTestImplementation Deps.Test.Android.JUNIT
    androidTestImplementation Deps.Test.Android.ESPRESSO
    androidTestImplementation Deps.Test.Android.ESPRESSO_INTENTS
    androidTestImplementation Deps.Test.Android.FRAGMENT
    androidTestImplementation Deps.Test.Android.NAVIGATION
    androidTestImplementation Deps.Kotlin.REFLECT
    debugImplementation Deps.Test.Android.FRAGMENT
}

defaultTasks 'clean', 'installZcashmainnetRelease'
